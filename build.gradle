buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}
repositories {
    maven {
        name = "chickenbones"
        url = "http://chickenbones.net/maven/"
    }
    maven {
        name "Mobius Repo"
        url "http://mobiusstrip.eu/maven"
    }
}

apply plugin: 'forge'
apply plugin: 'java'
apply plugin: 'sonar-runner'
apply plugin: 'jacoco'

ext.configFile = file "build.properties"

configFile.withReader {
    def prop = new Properties();
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

ext.modVersion = "${config.version.mod.major}.${config.version.mod.minor}.${config.version.mod.revis}"
version = "${config.version.minecraft}-${modVersion}"
group = "com.InfinityRaider.AgriCraft" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "AgriCraft"

minecraft {
    version = "${config.version.minecraft}-${config.version.forge}"
    runDir = "eclipse"

    replaceIn "reference/Reference.java"
    replace "@MAJOR@", config.version.mod.major
    replace "@MINOR@", config.version.mod.minor
    replace "@REVIS@", config.version.mod.revis
}

dependencies {
    // http://files.minecraftforge.net/CodeChickenLib/
    compile "codechicken:CodeChickenLib:1.7.10-1.1.1.104:dev"

    // http://chickenbones.net/maven
    compile "codechicken:CodeChickenCore:1.7.10-1.0.3.26:dev"
    compile "codechicken:NotEnoughItems:1.7.10-1.0.3.67:dev"

    // http://mobiusstrip.eu/maven
    compile "mcp.mobius.waila:Waila:1.5.5_1.7.10:dev"

    //some more mods
    compile fileTree(dir: 'libs', includes: ['*Mantle*.jar'])
    compile fileTree(dir: 'libs', includes: ['*natura*.jar'])
    compile fileTree(dir: 'libs', includes: ['*Harvestcraft*.jar'])
    compile fileTree(dir: 'libs', includes: ['*Pams Mob Drop Crops*.zip'])
    compile fileTree(dir: 'libs', includes: ['*magicalcrops*.jar'])
    compile fileTree(dir: 'libs', includes: ['*plantmegapack*.jar'])
    compile fileTree(dir: 'libs', includes: ['*ChocoCraft*.jar'])
    compile fileTree(dir: 'libs', includes: ['*TConstruct*.jar'])
    compile fileTree(dir: 'libs', includes: ['MineTweaker3-Dev*.jar'])

    // Test dependencies
    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:1.+"
}

allprojects {
    if (System.getenv("TRAVIS_BUILD_NUMBER") != null) {
        version += "-b${System.getenv("TRAVIS_BUILD_NUMBER")}"
    }
}

processResources {
    inputs.property "version", project.modVersion
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
            include 'mcmod.info'
            expand ([
                    'version': project.modVersion,
                    'mcversion': project.minecraft.version])
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}


